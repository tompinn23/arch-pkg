# Maintainer: Tom Pinnock <tomj.pinnock@outlook.com>


pkgname=(linux-firmware-dellxps13)
_pkgname="linux-firmware"
_commit=260cb35b11a968e7896f911565b75e411636ad69 # master
pkgver=20190409.260cb35
pkgrel=1
pkgdesc="Firmware files for Linux with modified firmware for dell xps 13 9360"
makedepends=('git')
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
options=(!strip)
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git#commit=${_commit}"
	"board.bin"
	)
md5sums=('SKIP'
         'df5ba1ca92f98bbd9cb537ddc5747011' )

prepare() {
  #cd "${srcdir}/${pkgname}"
  cp -f board.bin "${srcdir}/${_pkgname}/ath10k/QCA6174/hw3.0/"
#  cp -f board-2.bin "${srcdir}/${_pkgname}/ath10k/QCA6174/hw3.0/"
#  cp -f firmware-5.bin "${srcdir}/${_pkgname}/ath10k/QCA6174/hw3.0/"
#  cp -f notice_ath10k_firmware-5.txt "${srcdir}/${_pkgname}/ath10k/QCA6174/hw3.0/" 
}

pkgver() {
  cd "${srcdir}/${_pkgname}"

  # Commit date + short rev
  echo $(TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir -p kernel/x86/microcode
  cat ${_pkgname}/amd-ucode/microcode_amd*.bin > kernel/x86/microcode/AuthenticAMD.bin
  # Make the .bin reproducible 
  [ ! -z $SOURCE_DATE_EPOCH ] && touch -d @$SOURCE_DATE_EPOCH kernel/x86/microcode/AuthenticAMD.bin
  echo kernel/x86/microcode/AuthenticAMD.bin | bsdcpio -o -H newc -R 0:0 > amd-ucode.img
}

package() {
  provides=('linux-firmware')
  conflicts=('linux-firmware-git'
  	     'linux-firmware'
             'kernel26-firmware'
             'ar9170-fw'
             'iwlwifi-1000-ucode'
             'iwlwifi-3945-ucode'
             'iwlwifi-4965-ucode'
             'iwlwifi-5000-ucode'
             'iwlwifi-5150-ucode'
             'iwlwifi-6000-ucode'
             'rt2870usb-fw'
             'rt2x00-rt61-fw'
             'rt2x00-rt71w-fw')
  replaces=('kernel26-firmware'
            'ar9170-fw'
            'iwlwifi-1000-ucode'
            'iwlwifi-3945-ucode'
            'iwlwifi-4965-ucode'
            'iwlwifi-5000-ucode'
            'iwlwifi-5150-ucode'
            'iwlwifi-6000-ucode'
            'rt2870usb-fw'
            'rt2x00-rt61-fw'
            'rt2x00-rt71w-fw')

  cd "${srcdir}/${_pkgname}"

  make DESTDIR="${pkgdir}" FIRMWAREDIR=/usr/lib/firmware install
  rm "${pkgdir}/usr/lib/firmware/"{Makefile,README,configure,GPL-3}

  install -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -Dm644 LICEN* WHENCE "${pkgdir}/usr/share/licenses/linux-firmware/"

  # Trigger a microcode reload for configurations not using early updates
  install -d "${pkgdir}/usr/lib/tmpfiles.d"
  echo 'w /sys/devices/system/cpu/microcode/reload - - - - 1' \
    >"${pkgdir}/usr/lib/tmpfiles.d/${_pkgname}.conf"
}


# vim:set ts=2 sw=2 et:
